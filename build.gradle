plugins {
  // we don't really need this but it stops IDEA getting
  // confused about output paths
  id "java"
}

// ------------------------------------------------------------
// Tasks

task wrapper(type: Wrapper) {
  gradleVersion = '3.3'
}

// ------------------------------------------------------------
// Dependencies

// Make sure we've parsed subproject dependencies
evaluationDependsOnChildren()

// Map of all projects by artifact group and name
def declarationToProject = subprojects.collectEntries { p -> [toDeclaration(p), p] }

// Replace artifact dependencies with subproject dependencies, if possible
subprojects.each { p ->
  def changes = []
  p.configurations.each { c ->
    c.dependencies.each { d ->
      def sub = declarationToProject[toDeclaration(d)]
      if (sub != null) {
        changes.add({
            c.dependencies.remove(d)
            p.dependencies.add(c.name, sub)
        })
      }
    }
  }
  for (change in changes) {
    change()
  }
}


// ------------------------------------------------------------
// Helper functions

/**
 * Reads a Project or Dependency and creates a map containing
 * its group and name, which can be used as a lookup key
 */
def toDeclaration(p) {
  [ group:p.group, name:p.name ]
}

